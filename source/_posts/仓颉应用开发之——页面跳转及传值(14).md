---
title: 仓颉应用开发之——页面跳转及传值(14)
categories:
  - 开发
  - D-移动开发
  - HarmonyOS
  - 仓颉  
tags:
  - HarmonyOS
  - 仓颉应用
abbrlink: 53f9c04
date: 2024-09-14 08:34:06
---
## 一 概述

* Router路由说明
* 页面跳转(未传值)及返回示例
* 页面跳转传值示例

<!--more-->

## 二 Router路由说明

### 2.1 Router说明

* 仓颉提供了Router实现页面间的跳转
* 需要导入`import ohos.router.*`模块

### 2.2 Router相关方法

|  No  |                             方法                             |                 说明                 |
| :--: | :----------------------------------------------------------: | :----------------------------------: |
|  1   | push(url!: String, params!: String = "")<br>push(url!: String, params!: JsonObject)<br> |        跳转到应用内的指定页面        |
|  2   |  back(String, String)<br>back(String, JsonObject)<br>back()  |       返回上一页面或指定的页面       |
|  3   |               getParams()<br>getParamsObject()               | 获取发起跳转的页面往当前页传入的参数 |

## 三 页面跳转(未传值)及返回示例

1-启动页面index.cj

```
package ohos_app_cangjie_entry

import ohos.base.*
import ohos.component.*
import ohos.state_manage.*
import ohos.state_macro_manage.*
import ohos_app_cangjie_entry.pages.*
import ohos_app_cangjie_entry.bean.*
import ohos.preferences.*
import ohos.router.*

@Entry
@Component
class MyView {
  @State var message: String = "Hello Cangjie"

    func build() {
       Row {
            Column() {
                Text(this.message).fontSize(50).fontWeight(FontWeight.Bold)
                Button("Next").onClick {
                    evt => Router.push(url: "Second")
                }.fontSize(30).width(180).height(50).margin(top: 20)
            }.width(100.percent)
        }.height(100.percent)
    }
}
```

2-要跳转页面second.cj

```
package ohos_app_cangjie_entry.pages
import ohos.base.*
import ohos.component.*
import ohos.state_manage.*
import ohos.state_macro_manage.*
import ohos.router.*

@Entry
@Component
class Second {
    @State var message: String = "Hi there"

    func build() {
        Row {
            Column() {
                Text(this.message).fontSize(50).fontWeight(FontWeight.Bold)
                Button("Back").onClick {
                    evt => Router.back()
                }.fontSize(30).width(180).height(50).margin(top: 20)
            }.width(100.percent)
        }.height(100.percent)
    }
}
```

2-效果图

![][1]

## 四 页面跳转传值示例

### 4.1 传递String参数-push(String, String)

1-index.cj代码(传递String参数)

```
package ohos_app_cangjie_entry

import ohos.base.*
import ohos.component.*
import ohos.state_manage.*
import ohos.state_macro_manage.*
import ohos_app_cangjie_entry.pages.*
import ohos_app_cangjie_entry.bean.*
import ohos.router.*
import std.collection.*
import encoding.json.*
import serialization.serialization.*

@Entry
@Component
class MyView {
    @State var message: String = "Hello Cangjie"

    func build() {
        Row(){
          Column() {
                Text(this.message).fontSize(50).fontWeight(FontWeight.Bold)
                Button("Next").onClick {evt =>
                        Router.push(url: "Second",params:"张三")
                }.fontSize(30).width(180).height(50).margin(top: 20)
            }.width(100.percent)
        }
        .height(100.percent)
    }
}
```

2-second.cj(接受String参数)

```
package ohos_app_cangjie_entry.pages

import ohos.base.*
import ohos.component.*
import ohos.state_manage.*
import ohos.state_macro_manage.*
import ohos.router.*
import log.*
import std.collection.*
import encoding.json.*
import serialization.serialization.*

@Entry
@Component
class Second {
  @State var message: String = "Hi there"

  protected override func onPageShow() {
        AppLog.info("onPageShow")
        this.message= Router.getParams().getOrThrow();
    }

    func build() {
        Row {
            Column() {
                Text(this.message).fontSize(50).fontWeight(FontWeight.Bold)
                Button("Back").onClick {
                    evt => Router.back()
                }.fontSize(30).width(180).height(50).margin(top: 20)
            }.width(100.percent)
        }.height(100.percent)
    }
}
```

3-效果图

![][2]

### 4.2 传递JsonObject参数-push(String, JsonObject)

1-index.cj代码(传递JsonObject参数)

```
package ohos_app_cangjie_entry

import ohos.base.*
import ohos.component.*
import ohos.state_manage.*
import ohos.state_macro_manage.*
import ohos_app_cangjie_entry.pages.*
import ohos_app_cangjie_entry.bean.*
import ohos.router.*
import std.collection.*
import encoding.json.*
import serialization.serialization.*

@Entry
@Component
class MyView {
     @State var message: String = "Hello Cangjie"

    var str:String = ##"{"name": "zs","age": 30}"##;
    var jsv:JsonValue = JsonValue.fromStr(str);
    var jsobj:JsonObject = (jsv as JsonObject).getOrThrow();

    func build() {
        Row(){
          Column() {
                Text(this.message).fontSize(50).fontWeight(FontWeight.Bold)
                Button("Next").onClick {evt =>
                       Router.push(url: "Second",params:this.jsobj)
                }.fontSize(30).width(180).height(50).margin(top: 20)
            }.width(100.percent)
        }
        .height(100.percent)
    }
}
```

2-second.cj(接受JsonObject参数)

```
package ohos_app_cangjie_entry.pages

import ohos.base.*
import ohos.component.*
import ohos.state_manage.*
import ohos.state_macro_manage.*
import ohos.router.*
import log.*
import std.collection.*
import encoding.json.*
import serialization.serialization.*

@Entry
@Component
class Second {
    @State var message: String = "Hi there"
    @State var jsobj:JsonObject = JsonObject()

  protected override func onPageShow() {
        AppLog.info("onPageShow")
        this.message= Router.getParams().getOrThrow();
        this.jsobj = Router.getParamsObject()
    }

    func build() {
        Row {
            Column() {
                Text(getString(this.jsobj.get("name"))).fontSize(50).fontWeight(FontWeight.Bold)
                Button("Back").onClick {
                    evt => Router.back()
                }.fontSize(30).width(180).height(50).margin(top: 20)
            }.width(100.percent)
        }.height(100.percent)
    }
}
func getString(p: ?JsonValue): String{
    match (p) {
        case Some(x) => "${x}"
        case None => "none"
    }
}
```

3-效果图

![][3]



## 五 参考

* [HarmonyOS Beta5 仓颉—ohos.router](https://developer.huawei.com/consumer/cn/doc/cangjie-references-V5/cj-appendix-router-V5)



[1]:https://cdn.jsdelivr.net/gh/pgzxc/cdn/blog-hmos/cjapp-14-router-jump-1.gif
[2]:https://cdn.jsdelivr.net/gh/pgzxc/cdn/blog-hmos/cjapp-14-jump-string-2.gif
[3]:https://cdn.jsdelivr.net/gh/pgzxc/cdn/blog-hmos/cjapp-14-jump-jsonobj-3.gif
